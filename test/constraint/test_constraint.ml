open Core

let () =
  let open Async.Log.Global in
  For_testing.use_test_output ()
;;

open Mlsus_constraints
module C = Constraint
module T = C.Type

let print_solve_result ?(log_level = `Info) cst =
  Async.Log.Global.set_level log_level;
  let result = C.solve cst in
  match result with
  | Ok () -> print_s [%message "Constraint is satisfiable" (cst : Constraint.t)]
  | Error err ->
    print_s [%message "Constraint is unsatisfiable" (cst : Constraint.t) (err : Error.t)]
;;

let tint = T.constr [] "int"
let tstring = T.constr [] "string"

let%expect_test "Cannot unsuspend undetermined" =
  let open C in
  let a1 = T.Var.create () in
  let cst = exists a1 @@ match_ a1 ~closure:[ a1 ] ~with_:(fun _ -> T.var a1 =~ tint) in
  print_solve_result cst;
  [%expect
    {|
    (num_zombie_regions(state.num_zombie_regions 1))
    ("Constraint is unsatisfiable"
     (cst
      (Exists ((id 1) (name Type.Var))
       (Match ((id 1) (name Type.Var)) ((type_vars (((id 1) (name Type.Var)))))
        <fun>)))
     (err
      ("Failed to solve constraint"
       (exn (Mlsus_constraints__Generalization.Cannot_unsuspend_generic)))))
    |}]
;;

let%expect_test "Can unsuspend determined (pre)" =
  let open C in
  let a1 = T.Var.create () in
  let cst =
    exists a1
    @@ (T.(var a1 =~ tint)
        &~ match_ a1 ~closure:[] ~with_:(function
          | Constr ([], "int") -> tt
          | _ -> ff))
  in
  print_solve_result cst;
  [%expect
    {|
    ("Constraint is satisfiable"
     (cst
      (Exists ((id 2) (name Type.Var))
       (Conj (Eq (Var ((id 2) (name Type.Var))) (Structure (Constr () int)))
        (Match ((id 2) (name Type.Var)) ((type_vars ())) <fun>)))))
    |}]
;;

let%expect_test "Can unsuspend determined (post)" =
  let open C in
  let a1 = T.Var.create () in
  let cst =
    exists a1
    @@ (match_ a1 ~closure:[] ~with_:(function
          | Constr ([], "int") -> tt
          | _ -> ff)
        &~ T.(var a1 =~ tint))
  in
  print_solve_result cst;
  [%expect
    {|
    ("Constraint is satisfiable"
     (cst
      (Exists ((id 3) (name Type.Var))
       (Conj (Match ((id 3) (name Type.Var)) ((type_vars ())) <fun>)
        (Eq (Var ((id 3) (name Type.Var))) (Structure (Constr () int)))))))
    |}]
;;

let%expect_test "Cannot unsuspend circular dependencies" =
  let open C in
  let a1 = T.Var.create () in
  let a2 = T.Var.create () in
  let cst =
    exists a1
    @@ exists a2
    @@ (match_ a1 ~closure:[ a2 ] ~with_:(fun _ -> T.var a2 =~ tint)
        &~ match_ a2 ~closure:[ a1 ] ~with_:(fun _ -> T.var a1 =~ tint))
  in
  print_solve_result cst;
  [%expect
    {|
    (num_zombie_regions(state.num_zombie_regions 1))
    ("Constraint is unsatisfiable"
     (cst
      (Exists ((id 4) (name Type.Var))
       (Exists ((id 5) (name Type.Var))
        (Conj
         (Match ((id 4) (name Type.Var)) ((type_vars (((id 5) (name Type.Var)))))
          <fun>)
         (Match ((id 5) (name Type.Var)) ((type_vars (((id 4) (name Type.Var)))))
          <fun>)))))
     (err
      ("Failed to solve constraint"
       (exn (Mlsus_constraints__Generalization.Cannot_unsuspend_generic)))))
    |}]
;;

let%expect_test "Can unsuspend topological dependencies" =
  let open C in
  let a1 = T.Var.create () in
  let a2 = T.Var.create () in
  let cst =
    exists a1
    @@ exists a2
    @@ (T.(var a1 =~ tint)
        &~ match_ a1 ~closure:[ a2 ] ~with_:(fun _ -> T.var a2 =~ tint)
        &~ match_ a2 ~closure:[] ~with_:(fun _ -> tt))
  in
  print_solve_result cst;
  [%expect
    {|
    ("Constraint is satisfiable"
     (cst
      (Exists ((id 6) (name Type.Var))
       (Exists ((id 7) (name Type.Var))
        (Conj
         (Conj (Eq (Var ((id 6) (name Type.Var))) (Structure (Constr () int)))
          (Match ((id 6) (name Type.Var))
           ((type_vars (((id 7) (name Type.Var))))) <fun>))
         (Match ((id 7) (name Type.Var)) ((type_vars ())) <fun>))))))
    |}]
;;

let%expect_test "No suspended matches results in normal generalization" =
  let open C in
  (* Example constraint is for the program:
     let id = fun x -> x in
     id 1
  *)
  let a1 = T.Var.create () in
  let a2 = T.Var.create () in
  let a3 = T.Var.create () in
  let a4 = T.Var.create () in
  let a5 = T.Var.create () in
  let a6 = T.Var.create () in
  let xid = Var.create () in
  let xx = Var.create () in
  let cst =
    exists a1
    @@ let_
         xid
         #= (poly_scheme
               ([ a2 ]
                @. (exists a3
                    @@ exists a4
                    @@ (T.(var a2 =~ var a3 @-> var a4)
                        &~ let_ xx #= (mono_scheme (T.var a3)) ~in_:(inst xx (T.var a4)))
                   )
                @=> T.var a2))
         ~in_:
           (exists a5
            @@ exists a6
            @@ (inst xid (T.var a5)
                &~ T.(var a5 =~ var a6 @-> var a1)
                &~ T.(var a6 =~ tint)))
  in
  print_solve_result cst;
  [%expect
    {|
    ("Constraint is satisfiable"
     (cst
      (Exists ((id 8) (name Type.Var))
       (Let ((id 1) (name Constraint.Var))
        ((type_vars (((id 9) (name Type.Var))))
         (in_
          (Exists ((id 10) (name Type.Var))
           (Exists ((id 11) (name Type.Var))
            (Conj
             (Eq (Var ((id 9) (name Type.Var)))
              (Structure
               (Arrow (Var ((id 10) (name Type.Var)))
                (Var ((id 11) (name Type.Var))))))
             (Let ((id 2) (name Constraint.Var))
              ((type_vars ()) (in_ True) (type_ (Var ((id 10) (name Type.Var)))))
              (Instance ((id 2) (name Constraint.Var))
               (Var ((id 11) (name Type.Var)))))))))
         (type_ (Var ((id 9) (name Type.Var)))))
        (Exists ((id 12) (name Type.Var))
         (Exists ((id 13) (name Type.Var))
          (Conj
           (Conj
            (Instance ((id 1) (name Constraint.Var))
             (Var ((id 12) (name Type.Var))))
            (Eq (Var ((id 12) (name Type.Var)))
             (Structure
              (Arrow (Var ((id 13) (name Type.Var)))
               (Var ((id 8) (name Type.Var)))))))
           (Eq (Var ((id 13) (name Type.Var))) (Structure (Constr () int))))))))))
    |}]
;;

let%expect_test "Partial generic becomes instance" =
  let open C in
  let a1 = T.Var.create () in
  let a2 = T.Var.create () in
  let a3 = T.Var.create () in
  let x1 = Var.create () in
  let cst =
    exists a1
    @@ exists a2
    @@ let_
         x1
         #= (poly_scheme
               ([ a3 ]
                @. match_ a1 ~closure:[ a3; a2 ] ~with_:(fun _ ->
                  T.(var a3 =~ var a2) &~ T.(var a2 =~ tint))
                @=> T.var a3))
         ~in_:(inst x1 tint &~ T.(var a1 =~ tstring))
  in
  print_solve_result cst;
  [%expect
    {|
    ("Constraint is satisfiable"
     (cst
      (Exists ((id 14) (name Type.Var))
       (Exists ((id 15) (name Type.Var))
        (Let ((id 3) (name Constraint.Var))
         ((type_vars (((id 16) (name Type.Var))))
          (in_
           (Match ((id 14) (name Type.Var))
            ((type_vars (((id 15) (name Type.Var)) ((id 16) (name Type.Var)))))
            <fun>))
          (type_ (Var ((id 16) (name Type.Var)))))
         (Conj
          (Instance ((id 3) (name Constraint.Var)) (Structure (Constr () int)))
          (Eq (Var ((id 14) (name Type.Var))) (Structure (Constr () string)))))))))
    |}]
;;

let%expect_test "Partial generic becomes generic" =
  let open C in
  let a1 = T.Var.create () in
  let a2 = T.Var.create () in
  let a3 = T.Var.create () in
  let x1 = Var.create () in
  let cst =
    exists a1
    @@ let_
         x1
         #= (poly_scheme
               ([ a2 ]
                @. match_ a1 ~closure:[ a2 ] ~with_:(fun _ ->
                  exists a3 @@ T.(var a2 =~ var a3 @-> var a3))
                @=> T.var a2))
         ~in_:
           (inst x1 T.(tint @-> tint)
            &~ inst x1 T.(tstring @-> tstring)
            &~ T.(var a1 =~ tint))
  in
  print_solve_result cst;
  [%expect
    {|
    ("Constraint is satisfiable"
     (cst
      (Exists ((id 17) (name Type.Var))
       (Let ((id 4) (name Constraint.Var))
        ((type_vars (((id 18) (name Type.Var))))
         (in_
          (Match ((id 17) (name Type.Var))
           ((type_vars (((id 18) (name Type.Var))))) <fun>))
         (type_ (Var ((id 18) (name Type.Var)))))
        (Conj
         (Conj
          (Instance ((id 4) (name Constraint.Var))
           (Structure
            (Arrow (Structure (Constr () int)) (Structure (Constr () int)))))
          (Instance ((id 4) (name Constraint.Var))
           (Structure
            (Arrow (Structure (Constr () string)) (Structure (Constr () string))))))
         (Eq (Var ((id 17) (name Type.Var))) (Structure (Constr () int))))))))
    |}]
;;

(* Partial generic -> generic, instance is suspended *)
(* Partial generic -> instance, instance is suspended *)
